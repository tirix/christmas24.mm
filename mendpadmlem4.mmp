$theorem mendpadmlem4

* Lemma for mendpadm. The identity ` X .o. ( Y .o. X ) = Y ` holds.

h1::mendpadm.a       |- B = ( Base ` M )
h2::mendpadm.b       |- .o. = ( +g ` M )
h3::mendpadm.c       |- ( ph -> B =/= (/) )
h4::mendpadm.d       |- ( ph -> M e. Mgm )
h5::mendpadm.e         |- ( ph -> A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z )
h6::mendpadmlem1.1     |- ( ph -> Y e. B )
h7::mendpadmlem2.1   |- ( ph -> X e. B )
8:1,2,3,4,5,7,6:mendpadmlem2
                    |- ( ph -> E. t e. B ( Y .o. t ) = X )
9::oveq1                  |- ( x = Y -> ( x .o. y ) = ( Y .o. y ) )
10::oveq1                 |- ( x = Y -> ( x .o. ( z .o. y ) ) = ( Y .o. ( z .o. y ) ) )
11:9,10:oveq12d          |- ( x = Y -> ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = ( ( Y .o. y ) .o. ( Y .o. ( z .o. y ) ) ) )
12:11:eqeq1d            |- ( x = Y -> ( ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z <-> ( ( Y .o. y ) .o. ( Y .o. ( z .o. y ) ) ) = z ) )
13::oveq2                 |- ( y = t -> ( Y .o. y ) = ( Y .o. t ) )
14::oveq2                  |- ( y = t -> ( z .o. y ) = ( z .o. t ) )
15:14:oveq2d              |- ( y = t -> ( Y .o. ( z .o. y ) ) = ( Y .o. ( z .o. t ) ) )
16:13,15:oveq12d         |- ( y = t -> ( ( Y .o. y ) .o. ( Y .o. ( z .o. y ) ) ) = ( ( Y .o. t ) .o. ( Y .o. ( z .o. t ) ) ) )
17:16:eqeq1d            |- ( y = t -> ( ( ( Y .o. y ) .o. ( Y .o. ( z .o. y ) ) ) = z <-> ( ( Y .o. t ) .o. ( Y .o. ( z .o. t ) ) ) = z ) )
18::oveq1                  |- ( z = Y -> ( z .o. t ) = ( Y .o. t ) )
19:18:oveq2d              |- ( z = Y -> ( Y .o. ( z .o. t ) ) = ( Y .o. ( Y .o. t ) ) )
20:19:oveq2d             |- ( z = Y -> ( ( Y .o. t ) .o. ( Y .o. ( z .o. t ) ) ) = ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) )
21::id                   |- ( z = Y -> z = Y )
22:20,21:eqeq12d        |- ( z = Y -> ( ( ( Y .o. t ) .o. ( Y .o. ( z .o. t ) ) ) = z <-> ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) = Y ) )
23:12,17,22:rspc3v     |- ( ( Y e. B /\ t e. B /\ Y e. B ) -> ( A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z -> ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) = Y ) )
24:23:imp             |- ( ( ( Y e. B /\ t e. B /\ Y e. B ) /\ A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z ) -> ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) = Y )
25:6:ad2antrr         |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> Y e. B )
26::simplr            |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> t e. B )
27:6:ad2antrr         |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> Y e. B )
28:5:ad2antrr         |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z )
29:25,26,27,28,24:syl31anc
                     |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) = Y )
30::simpr              |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> ( Y .o. t ) = X )
31:30:oveq2d          |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> ( Y .o. ( Y .o. t ) ) = ( Y .o. X ) )
32:30,31:oveq12d     |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> ( ( Y .o. t ) .o. ( Y .o. ( Y .o. t ) ) ) = ( X .o. ( Y .o. X ) ) )
33:32,29:eqtr3d     |- ( ( ( ph /\ t e. B ) /\ ( Y .o. t ) = X ) -> ( X .o. ( Y .o. X ) ) = Y )
qed:33,8:r19.29a   |- ( ph -> ( X .o. ( Y .o. X ) ) = Y )

$= ( cv co wceq oveq2d vt wcel wa simpr oveq12d wral ad2antrr simplr w3a eqeq1d
   oveq1 oveq2 id eqeq12d rspc3v imp syl31anc eqtr3d mendpadmlem2 r19.29a ) AHU
   AQZIRZGSZGHGIRZIRZHSUAEAVAEUBZUCZVCUCZVBHVBIRZIRZVEHVHVBGVIVDIVGVCUDZVHVBGHI
   VKTUEVHHEUBZVFVLBQZCQZIRZVMDQZVNIRZIRZIRZVPSZDEUFCEUFBEUFZVJHSZAVLVFVCOUGZAV
   FVCUHWCAWAVFVCNUGVLVFVLUIWAWBVTWBHVNIRZHVQIRZIRZVPSVBHVPVAIRZIRZIRZVPSBCDHVA
   HEEEVMHSZVSWFVPWJVOWDVRWEIVMHVNIUKVMHVQIUKUEUJVNVASZWFWIVPWKWDVBWEWHIVNVAHIU
   LWKVQWGHIVNVAVPIULTUEUJVPHSZWIVJVPHWLWHVIVBIWLWGVBHIVPHVAIUKTTWLUMUNUOUPUQUR
   ABCDUAEFHGIJKLMNPOUSUT $.

$d .o. t
$d .o. x
$d .o. y
$d .o. z
$d t x
$d t y
$d x y
$d x z
$d y z
$d B t
$d B x
$d B y
$d B z
$d Y t
$d Y x
$d Y y
$d Y z
$d X t
$d X x
$d X y
$d X z
$d ph t
$d ph x
$d ph y
$d ph z
$d t z
