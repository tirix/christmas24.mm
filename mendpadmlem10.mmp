$theorem mendpadmlem10

* Lemma for mendpadm.  Multiplication is associative.

h1::mendpadm.a          |- B = ( Base ` M )
h2::mendpadm.b          |- .o. = ( +g ` M )
h3::mendpadm.c        |- ( ph -> B =/= (/) )
h4::mendpadm.d         |- ( ph -> M e. Mgm )
h5::mendpadm.e        |- ( ph -> A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z )
h6::mendpadm8.1       |- ( ph -> X e. B )
h7::mendpadm8.2        |- ( ph -> Y e. B )
h8::mendpadm10.3        |- ( ph -> Z e. B )
9:1,2:mgmcl           |- ( ( M e. Mgm /\ X e. B /\ Y e. B ) -> ( X .o. Y ) e. B )
10:4,6,7,9:syl3anc   |- ( ph -> ( X .o. Y ) e. B )
11:1,2:mgmcl           |- ( ( M e. Mgm /\ Z e. B /\ Y e. B ) -> ( Z .o. Y ) e. B )
12:4,8,7,11:syl3anc
                      |- ( ph -> ( Z .o. Y ) e. B )
13:1,2:mgmcl          |- ( ( M e. Mgm /\ X e. B /\ ( Z .o. Y ) e. B ) -> ( X .o. ( Z .o. Y ) ) e. B )
14:4,6,12,13:syl3anc
                     |- ( ph -> ( X .o. ( Z .o. Y ) ) e. B )
15::oveq1                 |- ( x = X -> ( x .o. y ) = ( X .o. y ) )
16::oveq1                 |- ( x = X -> ( x .o. ( z .o. y ) ) = ( X .o. ( z .o. y ) ) )
17:15,16:oveq12d         |- ( x = X -> ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = ( ( X .o. y ) .o. ( X .o. ( z .o. y ) ) ) )
18:17:eqeq1d            |- ( x = X -> ( ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z <-> ( ( X .o. y ) .o. ( X .o. ( z .o. y ) ) ) = z ) )
19::oveq2                 |- ( y = Y -> ( X .o. y ) = ( X .o. Y ) )
20::oveq2                  |- ( y = Y -> ( z .o. y ) = ( z .o. Y ) )
21:20:oveq2d              |- ( y = Y -> ( X .o. ( z .o. y ) ) = ( X .o. ( z .o. Y ) ) )
22:19,21:oveq12d         |- ( y = Y -> ( ( X .o. y ) .o. ( X .o. ( z .o. y ) ) ) = ( ( X .o. Y ) .o. ( X .o. ( z .o. Y ) ) ) )
23:22:eqeq1d            |- ( y = Y -> ( ( ( X .o. y ) .o. ( X .o. ( z .o. y ) ) ) = z <-> ( ( X .o. Y ) .o. ( X .o. ( z .o. Y ) ) ) = z ) )
24::oveq1                  |- ( z = Z -> ( z .o. Y ) = ( Z .o. Y ) )
25:24:oveq2d              |- ( z = Z -> ( X .o. ( z .o. Y ) ) = ( X .o. ( Z .o. Y ) ) )
26:25:oveq2d             |- ( z = Z -> ( ( X .o. Y ) .o. ( X .o. ( z .o. Y ) ) ) = ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) )
27::id                   |- ( z = Z -> z = Z )
28:26,27:eqeq12d        |- ( z = Z -> ( ( ( X .o. Y ) .o. ( X .o. ( z .o. Y ) ) ) = z <-> ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) = Z ) )
29:18,23,28:rspc3v     |- ( ( X e. B /\ Y e. B /\ Z e. B ) -> ( A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z -> ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) = Z ) )
30:29:imp             |- ( ( ( X e. B /\ Y e. B /\ Z e. B ) /\ A. x e. B A. y e. B A. z e. B ( ( x .o. y ) .o. ( x .o. ( z .o. y ) ) ) = z ) -> ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) = Z )
31:6,7,8,5,30:syl31anc
                     |- ( ph -> ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) = Z )
32:1,2,3,4,5,7,8:mendpadmlem9
                     |- ( ph -> ( Y .o. Z ) = ( Z .o. Y ) )
33:32:oveq2d        |- ( ph -> ( X .o. ( Y .o. Z ) ) = ( X .o. ( Z .o. Y ) ) )
34:31:oveq2d        |- ( ph -> ( ( X .o. Y ) .o. ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) ) = ( ( X .o. Y ) .o. Z ) )
35:1,2,3,4,5,10,14:mendpadmlem8a
                    |- ( ph -> ( ( X .o. Y ) .o. ( ( X .o. Y ) .o. ( X .o. ( Z .o. Y ) ) ) ) = ( X .o. ( Z .o. Y ) ) )
qed:33,35,34:3eqtr2rd
                   |- ( ph -> ( ( X .o. Y ) .o. Z ) = ( X .o. ( Y .o. Z ) ) )

$= ( co wcel mendpadmlem9 oveq2d cmgm mgmcl syl3anc mendpadmlem8a wceq wral w3a
   cv oveq1 oveq12d eqeq1d oveq2 id eqeq12d rspc3v imp syl31anc 3eqtr2rd ) AGHJ
   ISZISGJHISZISZGHISZVDVCISZISVDJISAVAVBGIABCDEFHJIKLMNOQRUAUBABCDEFVDVCIKLMNO
   AFUCTZGETZHETZVDETNPQEFGHIKLUDUEAVFVGVBETZVCETNPAVFJETZVHVINRQEFJHIKLUDUEEFG
   VBIKLUDUEUFAVEJVDIAVGVHVJBUJZCUJZISZVKDUJZVLISZISZISZVNUGZDEUHCEUHBEUHZVEJUG
   ZPQROVGVHVJUIVSVTVRVTGVLISZGVOISZISZVNUGVDGVNHISZISZISZVNUGBCDGHJEEEVKGUGZVQ
   WCVNWGVMWAVPWBIVKGVLIUKVKGVOIUKULUMVLHUGZWCWFVNWHWAVDWBWEIVLHGIUNWHVOWDGIVLH
   VNIUNUBULUMVNJUGZWFVEVNJWIWEVCVDIWIWDVBGIVNJHIUKUBUBWIUOUPUQURUSUBUT $.

$d X z
$d X x
$d X y
$d .o. z
$d .o. x
$d .o. y
$d Y z
$d Y y
$d Z z
$d x y
$d x z
$d y z
$d B x
$d B y
$d B z
$d M y
$d M z
$d ph x
$d ph y
$d ph z
$d Y x
$d Z x
$d Z y
